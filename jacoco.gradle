subprojects {
    if (!['example', 'android', 'android-test', 'jsr223', 'jpms-example'].contains(project.name)) {
        apply plugin: 'jacoco'

        jacocoTestReport {
            dependsOn fileTree(buildDir.absolutePath).include('jacoco/*.exec')

            executionData.setFrom(fileTree(project(':').buildDir.absolutePath).include('jacoco/*.exec'))

            reports {
                xml.required = true
                html.required = true
                csv.required = true
                xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
            }

            afterEvaluate {
                classDirectories.setFrom(files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: '**/party/iroiro/luajava/util/**')
                }))
            }
        }

        jacocoTestReport.shouldRunAfter project(':example').tasks.named('test')
    }
}
